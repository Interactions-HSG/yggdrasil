plugins {
  id 'application'
  id 'eclipse'
  id 'com.github.johnrengelman.shadow' version '5.1.0'
}

defaultTasks 'shadowJar'

repositories {
  jcenter()

  // maven { url "http://jacamo.sourceforge.net/maven2" }
  maven { url "https://raw.github.com/jacamo-lang/mvn-repo/master" }

  maven {url "https://jitpack.io"}
}

group = "org.hyperagents"
version = '0.0-SNAPSHOT'
targetCompatibility = "15"
sourceCompatibility = "15"
mainClassName = 'io.vertx.core.Launcher'

def vertxVersion = '3.9.0'
def mainVerticleName = 'org.hyperagents.yggdrasil.MainVerticle'
def watchForChange = 'src/**/*'
def doOnChange = './gradlew classes'

logging.captureStandardOutput LogLevel.INFO

dependencies {
  implementation "io.vertx:vertx-core:$vertxVersion"
  implementation "io.vertx:vertx-config:$vertxVersion"
  implementation "io.vertx:vertx-web:$vertxVersion"
  implementation "io.vertx:vertx-web-api-contract:$vertxVersion"
  implementation "io.vertx:vertx-web-client:$vertxVersion"
  implementation "io.github.classgraph:classgraph:4.4.12"

  implementation 'org.apache.httpcomponents.client5:httpclient5:5.0.1'
  implementation 'org.apache.httpcomponents.client5:httpclient5-fluent:5.0.1'

  implementation files("libs/cartago-3.1.jar")
  implementation "com.github.Interactions-HSG:wot-td-java:v0.1.1"

  // https://mvnrepository.com/artifact/org.eclipse.rdf4j/rdf4j-model
  implementation group: 'org.eclipse.rdf4j', name: 'rdf4j-model', version: '2.4.2'
  // https://mvnrepository.com/artifact/org.eclipse.rdf4j/rdf4j-repository-sail
  implementation group: 'org.eclipse.rdf4j', name: 'rdf4j-repository-sail', version: '2.4.2'
  // https://mvnrepository.com/artifact/org.eclipse.rdf4j/rdf4j-sail-memory
  implementation group: 'org.eclipse.rdf4j', name: 'rdf4j-sail-memory', version: '2.4.2'

  implementation group: 'com.mbed.java-coap', name: 'coap-core', version: '5.0.0'

  implementation 'org.apache.commons:commons-rdf-api:0.5.0'
  implementation 'org.apache.commons:commons-rdf-rdf4j:0.5.0'
  implementation 'com.google.code.gson:gson:2.8.6'

  implementation 'ch.qos.logback:logback-classic:1.2.9'
  testImplementation "junit:junit:4.12"
  testImplementation "io.vertx:vertx-unit:$vertxVersion"
  //testImplementation 'ch.qos.loback:logback-classic:1.2.9'
}

shadowJar {
  classifier = 'fat'
  manifest {
      attributes "Main-Verticle": mainVerticleName
  }
  mergeServiceFiles {
    include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
  }
}

run(({
  args = ['run', mainVerticleName, "--redeploy=$watchForChange", "--launcher-class=$mainClassName", "--on-redeploy=$doOnChange"]
}))


apply plugin: 'java'

compileJava {
  options.compilerArgs << '-parameters'
}
