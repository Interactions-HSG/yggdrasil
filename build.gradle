plugins {
  id 'application'
  id 'eclipse'
  id 'com.github.johnrengelman.shadow' version '5.1.0'
}

defaultTasks 'shadowJar'

repositories {
  mavenCentral()
  maven { url "https://raw.github.com/jacamo-lang/mvn-repo/master" }
}

group = "org.hyperagents"
version = '0.0-SNAPSHOT'
sourceCompatibility = JavaVersion.VERSION_11
mainClassName = 'io.vertx.core.Launcher'

def vertxVersion = '3.9.7'
def mainVerticleName = 'org.hyperagents.yggdrasil.MainVerticle'
def watchForChange = 'src/**/*'
def doOnChange = './gradlew classes'

dependencies {
  implementation "io.vertx:vertx-core:$vertxVersion"
  implementation "io.vertx:vertx-config:$vertxVersion"
  implementation "io.vertx:vertx-web:$vertxVersion"
  implementation "io.vertx:vertx-web-api-contract:$vertxVersion"
  implementation "io.vertx:vertx-web-client:$vertxVersion"
  implementation "io.github.classgraph:classgraph:4.4.12"

  implementation 'org.apache.httpcomponents.client5:httpclient5:5.0.1'
  implementation 'org.apache.httpcomponents.client5:httpclient5-fluent:5.0.1'

  implementation name: "cartago", version: "2.5-SNAPSHOT"
  implementation name: "wot-td-java", version: "v0.1.2"

  implementation 'org.eclipse.rdf4j:rdf4j-model:2.4.2'
  implementation 'org.eclipse.rdf4j:rdf4j-repository-sail:2.4.2'
  implementation 'org.eclipse.rdf4j:rdf4j-sail-memory:2.4.2'
  implementation 'org.eclipse.rdf4j:rdf4j-sail-nativerdf:2.4.2'

  implementation 'org.apache.commons:commons-rdf-api:0.5.0'
  implementation 'org.apache.commons:commons-rdf-rdf4j:0.5.0'
  implementation 'com.google.code.gson:gson:2.8.9'

  testImplementation 'junit:junit:4.13.1'
  testImplementation "io.vertx:vertx-unit:$vertxVersion"
}

shadowJar {
  manifest {
      attributes "Main-Verticle": mainVerticleName
  }
  mergeServiceFiles {
    include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
  }
}

run {
  args = ['run', mainVerticleName, "--redeploy=$watchForChange", "--launcher-class=$mainClassName", "--on-redeploy=$doOnChange"]
}

apply plugin: 'java'

compileJava {
  options.compilerArgs << '-parameters'
}
